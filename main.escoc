-- This does not type check because it lacks a whnf

inconsistent
: {P : Type} P
= inconsistent

-- Identity

the
: {A : Type} {u : A} A
= [A] [u] u

-- A self-referential type

Self
: {T : (Self T)} Type
= [T] {self : (T self)} Type

-- The empty, uninhabited type

Empty
: (Self Empty)
= [self]
  {Prop : {self : (Empty self)} Type}
  (Prop self)

-- The unit type

Unit
: (Self Unit)
= [self]
  {Prop : (Self Unit)}
  {new  : (Prop Unit.new)}
  (Prop self)

Unit.new
: (Unit Unit.new)
= [Prop] [new] new

-- The booleans true and false

Bool
: (Self Bool)
= [self]
  {Prop  : (Self Bool)}
  {true  : (Prop Bool.true)}
  {false : (Prop Bool.false)}
  (Prop self)

Bool.true
: (Bool Bool.true)
= [Prop] [true] [false] true

Bool.false
: (Bool Bool.false)
= [Prop] [true] [false] false

Bool.induct
: {self  : (Bool self)}
  {Prop  : (Self Bool)}
  {true  : (Prop Bool.true)}
  {false : (Prop Bool.false)}
  (Prop self)
= [self] [Prop] [true] [false]
  (self Prop true false)

Bool.not
: {self : (Bool self)} (Bool (Bool.not self))
= [self]
  (self
    [self](Bool (Bool.not self))
    Bool.false
    Bool.true)

-- Natural numbers (Scott)

Nat
: (Self Nat)
= [self]
  {Prop  : (Self Nat)}
  {succ  : {pred : (Nat pred)} (Prop (Nat.succ pred))}
  {zero  : (Prop Nat.zero)}
  (Prop self)

Nat.succ
: {pred : (Nat pred)} (Nat (Nat.succ pred))
= [pred] [Nat.] [succ.] [zero.]
  (succ. pred)

Nat.zero
: (Nat Nat.zero)
= [Nat.] [succ.] [zero.] zero.

Nat.0 : (Nat Nat.0) = Nat.zero
Nat.1 : (Nat Nat.1) = (Nat.succ Nat.0)
Nat.2 : (Nat Nat.2) = (Nat.succ Nat.1)
Nat.3 : (Nat Nat.3) = (Nat.succ Nat.2)
Nat.4 : (Nat Nat.4) = (Nat.succ Nat.3)
Nat.5 : (Nat Nat.5) = (Nat.succ Nat.4)
Nat.6 : (Nat Nat.6) = (Nat.succ Nat.5)
Nat.7 : (Nat Nat.7) = (Nat.succ Nat.6)
Nat.8 : (Nat Nat.8) = (Nat.succ Nat.7)
Nat.9 : (Nat Nat.9) = (Nat.succ Nat.8)

Nat.id
: {a : (Nat a)} (Nat (Nat.id a))
= [a]
  (a [a] (Nat (Nat.id a))
    [pred : (Nat pred)] (Nat.succ (Nat.id pred))
    Nat.zero)

Nat.same
: {a : (Nat a)} (Nat (Nat.same a))
= [a] [Prop] [succ] [zero]
  (a [a] (Prop (Nat.same a))
    [pred : (Nat pred)] (succ (Nat.same pred))
    zero)

Nat.double
: {n : (Nat n)} (Nat (Nat.double n))
= [n]
  (n [n] (Nat (Nat.double n))
    [pred : (Nat pred)] (Nat.succ (Nat.succ (Nat.double pred)))
    Nat.zero)

Nat.add
: {a : (Nat a)} {b : (Nat b)} (Nat (Nat.add a b))
= [a] [b]
  (a [a] (Nat (Nat.add a b))
    [~a : (Nat ~a)] (Nat.succ (Nat.add ~a b))
    b)

Nat.add_n_zero
: {n : (Nat n)} (Eq Nat (Nat.add n Nat.zero) n (Nat.add_n_zero n))
= [n]
  (n [self](Eq Nat (Nat.add self Nat.zero) self (Nat.add_n_zero self))
    [pred : (Nat pred)] (Eq.cong Nat Nat (Nat.add pred Nat.zero) pred (Nat.add_n_zero pred) Nat.succ)
    (Eq.refl Nat Nat.zero))

Nat.add_n_succ_m
: {n : (Nat n)} {m : (Nat m)} (Eq Nat (Nat.add n (Nat.succ m)) (Nat.succ (Nat.add n m)) (Nat.add_n_succ_m n m))
= [N] [M]
  (N
    [self] (Eq Nat (Nat.add self (Nat.succ M)) (Nat.succ (Nat.add self M)) (Nat.add_n_succ_m self M))
    [pred : (Nat pred)] (Eq.cong Nat Nat (Nat.add pred (Nat.succ M)) (Nat.succ (Nat.add pred M)) (Nat.add_n_succ_m pred M) Nat.succ)
    (Eq.refl Nat (Nat.succ M)))

Nat.add_comm
: {n : (Nat n)} {m : (Nat m)} (Eq Nat (Nat.add n m) (Nat.add m n) (Nat.add_comm n m))
= [n : (Nat n)]
  (n [self : (Nat self)] {m : (Nat m)} (Eq Nat (Nat.add self m) (Nat.add m self) (Nat.add_comm self m))
     [pred : (Nat pred)] [m : (Nat m)]
       (Eq.subst Nat
	 (Nat.add m pred) (Nat.add pred m) (Eq.sym Nat (Nat.add pred m) (Nat.add m pred) (Nat.add_comm pred m))
	 [x : (Nat x)] (Eq Nat (Nat.succ x) (Nat.add m (Nat.succ pred)))
	 (Eq.sym Nat (Nat.add m (Nat.succ pred)) (Nat.succ (Nat.add m pred)) (Nat.add_n_succ_m m pred)))
     [m : (Nat m)] (Eq.sym Nat (Nat.add m Nat.zero) m (Nat.add_n_zero m)))

-- Natural numbers (Church)

Cat
: (Self Cat)
= [self]
  {P : (Self P)}
  {s : {x : (P x)} (P (s x))}
  {z : (P z)}
  (P (self P s z))

Cat.succ
: {n : (Cat n)} (Cat (Cat.succ n))
= [n] [P] [s] [z]
  (s (n P s z))

Cat.zero
: (Cat Cat.zero)
= [P] [s] [z]
  z

Cat.0 : (Cat Cat.0) = Cat.zero
Cat.1 : (Cat Cat.1) = (Cat.succ Cat.0)
Cat.2 : (Cat Cat.2) = (Cat.succ Cat.1)
Cat.3 : (Cat Cat.3) = (Cat.succ Cat.2)
Cat.4 : (Cat Cat.4) = (Cat.succ Cat.3)
Cat.5 : (Cat Cat.5) = (Cat.succ Cat.4)
Cat.6 : (Cat Cat.6) = (Cat.succ Cat.5)
Cat.7 : (Cat Cat.7) = (Cat.succ Cat.6)
Cat.8 : (Cat Cat.8) = (Cat.succ Cat.7)
Cat.9 : (Cat Cat.9) = (Cat.succ Cat.8)

Cat.add
: {n : (Cat n)} {m : (Cat m)} (Cat (Cat.add n m))
= [n] [m] [P] [s] [z]
  (n P s (m P s z))

Cat.mul
: {n : (Cat n)} {m : (Cat m)} (Cat (Cat.mul n m))
= [n] [m] [P] [s] [z]
  (n P (m P s) z)

Cat.ex
: (Eq Cat Cat.zero Cat.zero (Eq.refl Cat Cat.zero))
= (Eq.refl Cat Cat.zero)

Cat.even
: {n : (Cat n)} (Bool (Cat.even n))
= [n] 
  (n [n : (Bool n)](Bool n)
    [n : (Bool n)] (Bool.not n)
    Bool.true)

-- Natural numbers (Parigot)

Rat
: {self : (Rat self)} Type
= [self]
  {Prop : {self : (Rat self)} Type}
  {succ : {pred : (Rat pred)} {fold : (Prop pred)} (Prop (Rat.succ pred))}
  {zero : (Prop Rat.zero)}
  (Prop self)

Rat.succ
: {pred : (Rat pred)} (Rat (Rat.succ pred))
= [pred] [Rat.] [succ.] [zero.]
  (succ. pred (pred Rat. succ. zero.))

Rat.zero
: (Rat Rat.zero)
= [Rat.] [succ.] [zero.] zero.

Rat.0 : (Rat Rat.0) = Rat.zero
Rat.1 : (Rat Rat.1) = (Rat.succ Rat.0)
Rat.2 : (Rat Rat.2) = (Rat.succ Rat.1)
Rat.3 : (Rat Rat.3) = (Rat.succ Rat.2)
Rat.4 : (Rat Rat.4) = (Rat.succ Rat.3)
Rat.5 : (Rat Rat.5) = (Rat.succ Rat.4)
Rat.6 : (Rat Rat.6) = (Rat.succ Rat.5)
Rat.7 : (Rat Rat.7) = (Rat.succ Rat.6)
Rat.8 : (Rat Rat.8) = (Rat.succ Rat.7)
Rat.9 : (Rat Rat.9) = (Rat.succ Rat.8)

Rat.add
: {n : (Rat n)}
  {m : (Rat m)}
  (Rat (Rat.add n m))
= [n] (n
  [self : (Rat self)] {m : (Rat m)} (Rat (Rat.add self m))
  [pred] [fold : {m : (Rat m)} (Rat (Rat.add pred m))] [m : (Rat m)] (fold (Rat.succ m))
  [m : (Rat m)] m)

Rat.double_aux
: {n : (Rat n)}
  {m : (Rat m)}
  (Rat (Rat.double_aux n m))
= [n] (n
  [self : (Rat self)]
    {m : (Rat m)} (Rat (Rat.double_aux self m))
  [pred] [fold : {m : (Rat m)} (Rat (Rat.double_aux pred m))]
    [m : (Rat m)] (fold (Rat.succ (Rat.succ m)))
  [m : (Rat m)] m)

Rat.double
: {n : (Rat n)}
  (Rat (Rat.double n))
= [n] (Rat.double_aux n Rat.zero)

Rat.to_cat
: {n : (Rat n)} (Cat (Rat.to_cat n))
= [n] (n
  [self : (Rat self)] (Cat (Rat.to_cat self))
  [pred]
    [fold : (Cat (Rat.to_cat pred))]
    [P : (Self P)]
    [s : {x : (P x)} (P (s x))]
    [z : (P z)]
    (fold P s (s z))
  Cat.zero)

Rat.from_cat
: {n : (Cat n)} (Rat (Rat.from_cat n))
= [n] (n Rat Rat.succ Rat.zero)

Rat.add2
: {n : (Rat n)} {m : (Rat m)} (Rat (Rat.add2 n m))
= [n] [m] (Rat.from_cat (Cat.add (Rat.to_cat n) (Rat.to_cat m)))

Rat.mul2
: {n : (Rat n)} {m : (Rat m)} (Rat (Rat.mul2 n m))
= [n] [m] (Rat.from_cat (Cat.mul (Rat.to_cat n) (Rat.to_cat m)))

-- Equality on self-referential types

Eq
: {T    : (Self T)}
  {a    : (T a)}
  {b    : (T b)}
  {self : (Eq T a b self)}
  Type
= [T] [a] [b] [self]
  {Prop : {b : (T b)} {self : (Eq T a b self)} Type}
  {refl : (Prop a (Eq.refl T a))}
  (Prop b self)

Eq.refl
: {T : (Self T)}
  {a : (T a)}
  (Eq T a a (Eq.refl T a))
= [T] [a] [Prop] [refl]
  refl

Eq.sym
: {T : {self : (T self)} Type}
  {a : (T a)}
  {b : (T b)}
  {e : (Eq T a b e)}
  (Eq T b a (Eq.sym T a b e))
= [T] [a] [b] [e]
  (e [b][self](Eq T b a (Eq.sym T a b self))
    (Eq.refl T a))

Eq.cong
: {A : {self : (A self)} Type}
  {B : {self : (B self)} Type}
  {a : (A a)}
  {b : (A b)}
  {e : (Eq A a b e)}
  {f : {a : (A a)} (B (f a))}
  (Eq B (f a) (f b) (Eq.cong A B a b e f))
= [A] [B] [a] [b] [e] [f]
  (e [b][self](Eq B (f a) (f b) (Eq.cong A B a b self f))
    (Eq.refl B (f a)))

Eq.subst
: {T : (Self T)}
  {a : (T a)}
  {b : (T b)}
  {e : (Eq T a b e)}
  {P : {a : (T a)} (Self (P a))}
  {x : (P a x)}
  (P b (Eq.subst T a b e P x))
= [T] [a] [b] [e] [P] [x]
  (e [b][self](P b (Eq.subst T a b self P x)) x)

-- Binary

Bin
: (Self Bin)
= [self]
  {Prop : (Self Bin)}
  {O    : {pred : (Bin pred)} (Prop (Bin.O pred))}
  {I    : {pred : (Bin pred)} (Prop (Bin.I pred))}
  {E    : (Prop Bin.E)}
  (Prop self)

Bin.O
: {pred : (Bin pred)}
  (Bin (Bin.O pred))
= [pred] [Prop] [O] [I] [E]
  (O pred)

Bin.I
: {pred : (Bin pred)}
  (Bin (Bin.I pred))
= [pred] [Prop] [O] [I] [E]
  (I pred)

Bin.E
: (Bin Bin.E)
= [Prop] [O] [I] [E]
  E

Bin.inc
: {x : (Bin x)}
  (Bin (Bin.inc x))
= [xs]
  (xs [self : (Bin self)] (Bin (Bin.inc self))
    [pred] (Bin.I pred)
    [pred] (Bin.O (Bin.inc pred))
    Bin.E)

-- Dependent pairs

Sigma
: {A : (Self A)}
  {B : {x : (A x)} (Self (B x))}
  {self : (Sigma A B self)}
  Type
= [A] [B] [self]
  {Prop : {self : (Sigma A B self)} Type}
  {new  : {a : (A a)} {b : (B a b)} (Prop (Sigma.new A B a b))}
  (Prop self)

Sigma.new
: {A : (Self A)}
  {B : {x : (A x)} (Self (B x))}
  {a : (A a)}
  {b : (B a b)}
  (Sigma A B (Sigma.new A B a b))
= [A] [B] [a] [b] [Prop] [new]
  (new a b)

Sigma.fst
: {A : (Self A)}
  {B : {x : (A x)} (Self (B x))}
  {sigma : (Sigma A B sigma)}
  (A (Sigma.fst A B sigma))
= [A] [B] [sigma]
  (sigma
    [self : (Sigma A B self)](A (Sigma.fst A B self))
    [a : (A a)] [b : (B a b)] a)

Sigma.snd
: {A : (Self A)}
  {B : {x : (A x)} (Self (B x))}
  {sigma : (Sigma A B sigma)}
  (B (Sigma.fst A B sigma) (Sigma.snd A B sigma))
= [A] [B] [sigma]
  (sigma
    [self : (Sigma A B self)] (B (Sigma.fst A B self) (Sigma.snd A B self))
    [a : (A a)] [b : (B a b)] b)

Sigma.example
= (Sigma.new Nat [x : (Nat x)](Eq Nat x x) Nat.3 (Eq.refl Nat Nat.3))

main
= Rat.mul2
